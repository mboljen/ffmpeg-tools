#!/usr/bin/env perl
#
# Created:        So 2024-12-15 09:23:41 CET
# Last Modified:  So 2025-03-16 11:12:35 CET
#
# ffmpeg-splitabook-editor:
#   Create, edit, save JSON tracklist manually

use strict;
use warnings;

use Data::Dumper;
use File::stat;
use File::Copy;
use File::Slurp;
use Getopt::Long;
use List::Util qw(first min max);
use JSON::PP;
use Term::ReadKey;
use Pod::Usage;
use POSIX;
use Regexp::Common;

# Declare options
my ($help,$man,$time_base0,$total) = (0,0,'1/1000');

# Process options from command line
GetOptions(
    'total=s'    => \$total,
    'timebase=s' => \$time_base0,
    'help|?'     => \$help,
    'man'        => \$man,
) or pod2usage(2);

# Show help
pod2usage(1) if $help;
pod2usage(-exitval => 0, -verbose => 2) if $man;

# Auto-update total time
my $updatetotal = not defined $total;

# Check time base (allow fractions and float-point numbers)
die "Invalid time base" unless $time_base0 =~ m/^\d+\/\d+$/ or
                               $time_base0 =~ m/^$RE{num}{real}$/;

# Check timebase accuracy
die "Inaccurate time base" if eval($time_base0) < 0 or eval($time_base0) > 1;

#
my $needbackup = 1;
my $ismodified = 0;
my $ref;

# Convert total time according to time base
$total = time2value($total,$time_base0) unless $updatetotal;

#
my $jsonfile = shift @ARGV;

#
die "Filename missing" unless defined $jsonfile;

# Create JSON object
my $json = JSON::PP->new->utf8->pretty->allow_nonref;

if (-f $jsonfile)
{
    # Read from file
    my $jsontext = read_file($jsonfile);

    # Check input

    # Decode input
    $ref = $json->decode($jsontext);
}
else
{
    # Initialize empty Perl reference
    $ref = { 'chapters' => [] };
}

#
my $command;

# Get Control characters
my %ctrl = GetControlChars();

# Start pseudo-loop
READCOM: while (not defined $command)
{
    # Initialize ID
    my $id = 0;

    # Sort track table
    foreach my $href (sort { $a->{start} <=> $b->{start} } @{$ref->{chapters}})
    {
        # Update ID
        $href->{id} = $id++;

        # Update start and end times
        $href->{start_time} = value2time($href->{start}, $href->{time_base});
        $href->{end_time}   = value2time($href->{end},   $href->{time_base});
    }

    # Reset ID
    $id = undef;

    #
    $total = max_end_time() if $updatetotal;

    # Fetch ID list
    my @allow = tracklist();

    #
    show() if scalar @allow;

    #
    printf "\n" .
        "FFMPEG-SPLITABOOK-EDITOR\n" .
        "stats: %d tracks, total time: %s\n\n" .
        " [a] ... add\n" .
        "%s" .
        " [s] ... save\n" .
        " [q] ... quit\n\n",
            scalar keys @{$ref->{chapters}}, value2time($total, $time_base0),
            scalar @allow > 0 ? " [e] ... edit\n".
                                " [r] ... remove\n" : "";

    #
    print "Enter command [quit] : ";
    while (not defined $command)
    {
        # Wait for command key to be pressed
        ReadMode 4;
        while (not defined ($command = ReadKey(0)))
        {
            last if $command eq $ctrl{EOF}
                 or $command eq $ctrl{EOL}
                 or $command eq $ctrl{QUIT}
                 or $command eq $ctrl{INTERRUPT}
                 or $command eq "\n"
                 or $command eq "\e";
        }
        ReadMode 0;

        #
        if    ($command =~ /^a/i)                   { $command = 'add';    }
        elsif ($command =~ /^e/i and scalar @allow) { $command = 'edit';   }
        elsif ($command =~ /^s/i)                   { $command = 'save';   }
        elsif ($command =~ /^r/i and scalar @allow) { $command = 'remove'; }
        elsif ($command =~ /^q/i
                or ord($command)==3
                or ord($command)==27)               { $command = 'quit';   }
        else
        {
            # Reset input, redo loop
            $command = undef;
        }
    }

    # Print verified command
    print "$command ";

    #
    if ($command =~ m/^(?:edit|remove)$/i)
    {
        # Get track ID
        my $key;
        while (not defined $id or $id eq '')
        {
            #
            ReadMode 4;
            while (not defined ($key = ReadKey(0)))
            {
                last if $key eq $ctrl{EOF}
                     or $key eq $ctrl{EOL}
                     or $key eq $ctrl{QUIT}
                     or $key eq $ctrl{INTERRUPT}
                     or $key eq "\n"
                     or $key eq "\e";
            }
            ReadMode 0;

            #
            if (ord($key)==3)
            {
                # Strg-C pressed, reset ID
                print "STRG-C\n";
                exit;
            }
            elsif (ord($key)==10)
            {
                # RETURN pressed, break loop
                if (defined $id and trackexists($id-1))
                {
                    print "\n";
                    last;
                }
            }
            elsif (ord($key)==27)
            {
                # ESC pressed
                $command = undef;
                print "ESC\n";
                $id = undef;
                last;
            }
            elsif (ord($key)==127)
            {
                # DEL pressed, remove last character
                if (defined $id and length($id))
                {
                    #
                    $id = substr($id,0,length($id)-1);
                    print chr(0x08) . ' ' . chr(0x08);
                }
                $id = undef if $id eq '';
                redo;
            }
            elsif ($key =~ m/\d/)
            {
                # Digit
                chomp($key);
                $id .= $key;
                print $key;
                redo;
            }
            else
            {
                # Invalid character, do nothing
                redo;
            }
        }

        # Assure that key exists
        if (not defined $id)
        {
            $command = undef;
            redo READCOM;
        }
    }

    #
    if (lc($command) eq 'add')
    {
        #
        print "\n";

        #
        edit(-1);
    }
    elsif (lc($command) eq 'edit')
    {
        #
        edit($id-1);
    }
    elsif (lc($command) eq 'remove')
    {
        # Relocate index of record
        my $idx = first { $ref->{chapters}[$_]{id} eq $id-1 } 0 .. $#{$ref->{chapters}};

        # Remove element KEY from array
        splice @{$ref->{chapters}},$idx,1;

        #
        $ismodified = 1;
    }
    elsif (lc($command) eq 'save')
    {

        print "\n";

        # Create backup of original JSON file unless already done
        if ($needbackup)
        {
            move($jsonfile,$jsonfile.'.bak') or die "Failed to create backup: $!";
            $needbackup = 0;
        }

        # Encode data to JSON text
        my $jsontext = $json->encode($ref);

        # Overwrite JSON file with JSON text
        write_file($jsonfile,$jsontext) or die "Failed to save file: $!";

        # Reset modification flag
        $ismodified = 0;

        # Write status message
        printf "\nFile '%s' successfully created: %d bytes written\n",
            $jsonfile, stat($jsonfile)->size;

    }
    elsif (lc($command) eq 'quit')
    {

        print "\n";

        # Terminate if not modified yet
        last unless $ismodified;

        # Confirm quit
        print "\n" .
            "WARNING\n" .
            "All modifications will be lost.  Please confirm [yn] ? ";

        my ($key,$enter);
        while (not defined $enter)
        {
            ReadMode 4;
            while (not defined ($key = ReadKey(0)))
            {
                last if $key eq $ctrl{EOF}
                     or $key eq $ctrl{EOL}
                     or $key eq $ctrl{QUIT}
                     or $key eq $ctrl{INTERRUPT}
                     or $key eq "\n"
                     or $key eq "\e";
            }
            ReadMode 0;

            if ($key =~ m/^y/i)
            {
                # Confirm
                $enter = 'yes';
            }
            else
            {
                # Do not confirm
                $enter = 'no';
            }
        }

        # Print response
        print $enter . "\n";

        # Terminate if confirmation granted
        last if $enter eq 'yes';

    }
    else
    {

        # Raise warning
        print "Unknown command\n";

    }

    # Reset command
    $command = undef;
}

# ------------------------------------------------------------------------------

sub show
{
    # Detect highest track number and longest track title
    my ($mxlen_id,$mxlen_title) = (0,0);
    foreach my $href (@{$ref->{chapters}})
    {
        # Update track number
        $mxlen_id = length($href->{id})+1
                 if length($href->{id})+1 > $mxlen_id+1;

        # Update length of longest track title
        $mxlen_title = length($href->{tags}{title})
                    if length($href->{tags}{title}) > $mxlen_title;
    }

    # Print header
    printf "\n  %" . $mxlen_id . "s %-" . $mxlen_title . "s %15s %15s %15s\n",
        'ID', 'TITLE', 'FROM', 'TO', 'DUR';

    # Print list of records
    foreach my $href (sort { $a->{id} <=> $b->{id} } @{$ref->{chapters}})
    {
        # Print item
        printf " [%".$mxlen_id."d] %-".$mxlen_title."s %15s %15s %15s\n",
            $href->{id}+1,
            $href->{tags}{title},
            value2time($href->{start},                $href->{time_base}),
            value2time($href->{end},                  $href->{time_base}),
            value2time($href->{end} - $href->{start}, $href->{time_base}),
    }

    # Checks
    foreach my $href (@{$ref->{chapters}})
    {
        #
        my $hbase  = $href->{time_base};
        my $hstart = $href->{start};
        my $hend   = $href->{end};

        #
        next unless defined $hstart and defined $hend;

        #
        printf "\nWARNING\nInvalid time range #%d : %s < %s",
                    $href->{id}, value2time($hstart, $hbase),
                                 value2time($hend,   $hbase) unless $hstart <= $hend;

        #
        foreach my $cref (@{$ref->{chapters}})
        {
            #
            next if $href eq $cref;

            #
            my $cstart = $cref->{start};
            my $cend   = $cref->{end};

            next unless defined $cstart and defined $cend;

            printf "\nWARNING\nOverlapping time ranges #%d and #%d",
                        $href->{id}, $cref->{id}
                            if ($hstart < $cstart and $hend > $cstart) or
                               ($hstart > $cstart and $cend > $hstart);
        }
    }
}

# ------------------------------------------------------------------------------

sub readstdin
{
    #
    my ($prompt,$default,$allow) = @_;
    #
    $prompt  = 'User input :' unless defined $prompt;
    $default = ''             unless defined $default;
    #
    die "Reference missing" unless defined $ref;
    #
    my $enter = $default;
    #
    print $prompt, $enter;
    #
    $allow = '\w' unless defined $allow;
    #
    my ($result,$key);
    #
    while (not defined $result)
    {
        #
        ReadMode 4;
        while (not defined ($key = ReadKey(0)))
        {
             last if $key eq $ctrl{EOF}
                  or $key eq $ctrl{EOL}
                  or $key eq $ctrl{QUIT}
                  or $key eq $ctrl{INTERRUPT}
                  or $key eq "\n"
                  or $key eq "\e";
        }
        ReadMode 0;

        #
        if (ord($key)==3)
        {
            # STRG-C pressed
            $result = 2;
            print "\nSTRG-C\n";
            last;
        }
        elsif (ord($key)==10)
        {
            # RETURN pressed, break loop
            $result = 0;
            print "\n";
            last;
        }
        elsif (ord($key)==27)
        {
            # ESC pressed, reset user input
            if (length($enter)>0)
            {
                print '' . ( chr(0x08) . ' ' . chr(0x08) ) x length($enter);
                $enter = '';
                redo;
            }
            else
            {
                $result = 1;
                print "\nESC\n";
                last;
            }
        }
        elsif (ord($key)==127)
        {
            # DEL pressed, remove last character
            if (length($enter)>0)
            {
                #
                $enter = substr($enter,0,length($enter)-1);
                print chr(0x08) . ' ' . chr(0x08);
            }
            redo;
        }
        else
        {
            if ($key =~ m/$allow/)
            {
                # Digit
                chomp($key);
                $enter .= $key;
                print $key;
                redo;
            }
            else
            {
                # Invalid character, do nothing
                redo;
            }
        }
    }

    # Update content of passed variable
    $_[1] = $enter;

    # Return exit code
    $result;
}

# ------------------------------------------------------------------------------

sub edit
{
    # Receive arguments
    my ($id,$time_base) = @_;

    # Set timebase to default unless defined
    $time_base = $time_base0 unless defined $time_base;

    # New record flag
    my $new = 0;

    # Prepare new record
    if (not defined $id or $id == -1)
    {
        # Enable new record flag
        $new = 1;

        # Get tracklist
        my @id = tracklist();

        # Set ID to 0 or last entry
        $id = (scalar @id > 0) ? max(@id)+1 : 0;

        # Add new record to chapter list
        push @{$ref->{chapters}}, {
            id         => $id,
            time_base  => $time_base,
            start      => max_end_time(),
            end        => undef,
            start_time => undef,
            end_time   => undef,
            tags       => { title => '' },
        };
    }

    # Relocate index of record
    my $idx = first { $ref->{chapters}[$_]{id} eq $id } 0 .. $#{$ref->{chapters}};

    # Create backup of record
    my $backup = $ref->{chapters}[$idx];

    # Set defaults
    my $title      = $backup->{tags}{title};
    my $start_time = $backup->{start_time};
    my $end_time   = $backup->{end_time};

    # Set start time to latest end time unless defined
    $start_time = value2time($backup->{start}, $backup->{time_base})
        unless defined $start_time;

    #
    print '' . ($new) ? "\nADD TRACK\n\n" : sprintf("\nEDIT TRACK #%d\n\n",$id+1);

    # Read track title
    if (readstdin("Edit track title : ", $title, '[\w\W]') > 0)
    {
        # ESC or STRG-C pressed, remove record if previously added
        splice @{$ref->{chapters}},$idx,1 if $new;
        return;
    }

    # Read start time
    if (readstdin("Edit start time  : ", $start_time, '[\d\:\.]') > 0)
    {
        # ESC or STRG-C pressed, remove record if previously added
        splice @{$ref->{chapters}},$idx,1 if $new;
        return;
    }

    # Set end time to start time unless defined
    $end_time = $start_time unless defined $end_time;

    # Read end time
    if (readstdin("Edit end time    : ", $end_time, '[\d\:\.+]') > 0)
    {
        # ESC or STRG-C pressed, remove record if previously added
        splice @{$ref->{chapters}},$idx,1 if $new;
        return;
    }

    #
    if ($end_time =~ m/^\+/)
    {
        #
        my $t1 = time2value($start_time, $backup->{time_base});
        my $dt = time2value($end_time,   $backup->{time_base});
        #
        my $t2 = $t1 + $dt;
        #
        $end_time = value2time($t2, $backup->{time_base});
    }

    # Notify changes
    $ismodified = 1
         if (defined $backup->{tags}{title} and $backup->{tags}{title} ne $title) or
            (defined $backup->{start_time}  and $backup->{start_time}  ne $start_time) or
            (defined $backup->{end_time}    and $backup->{end_time}    ne $end_time);

    # Update record
    $ref->{chapters}[$idx]{tags}{title} = $title;

    # Update track times
    $ref->{chapters}[$idx]{start} = time2value($start_time, $backup->{time_base});
    $ref->{chapters}[$idx]{end}   = time2value($end_time,   $backup->{time_base});
}

# ------------------------------------------------------------------------------

# Fetch all available ID
sub tracklist
{
    my @list = map { $_->{id} if defined $_->{id} } @{$ref->{chapters}};
    return @list;
}

# ------------------------------------------------------------------------------

# Check if ID exists
sub trackexists
{
    my $result = 0;
    if (defined $_[0])
    {
        my %id = map { $_ => 1 } tracklist();
        $result = exists($id{$_[0]});
    }
    return $result;
}

# ------------------------------------------------------------------------------

#
sub max_end_time
{
    my $result = 0;
    foreach my $href (@{$ref->{chapters}})
    {
        #
        my $totalsecs = $href->{end};
        #
        $result = $totalsecs if not defined $result or $totalsecs > $result;
    }
    #
    return $result;
}

# ------------------------------------------------------------------------------

# Convert numerical value to time string
sub value2time
{
    #
    my ($value,$time_base) = @_;

    #
    my $totalsecs = $value * eval($time_base);

    #
    my $hrs = int($totalsecs/3600);
    my $remainder = $totalsecs % 3600;
    my $mins = int($remainder/60);
    my $secs = $totalsecs - $hrs * 3600 - $mins * 60;

    #
    unless (defined $time_base)
    {
        # Auto-detect TIME_BASE
        $time_base = 1;
        $remainder = $secs - int($secs);
        #
        while ($remainder > 0 and
               log($time_base)/log(10) > -10 and
               int(log($time_base)/log(10)) == int(log($remainder)/log(10)))
        {
            $time_base /= 10;
            $remainder = $remainder * 10 - int($remainder * 10);
        }
    }

    #
    my $digs = max(ceil(abs(log(eval($time_base))/log(10))), 3);

    # Build time string
    my $str = '';
    $str .= sprintf("%d:",$hrs) if $hrs > 0;
    $str .= sprintf("%02d:",$mins);
    $str .= '0' if $secs < 10;
    $str .= sprintf("%.".$digs."f",$secs);

    # Return time string
    return $str;
}

# ------------------------------------------------------------------------------

# Convert time string to numerical value
sub time2value
{
    #
    my ($str,$time_base) = @_;

    $time_base = 1 unless defined $time_base;

    #
    my ($hrs,$mins,$secs) =
       ($str =~ m/^(?:($RE{num}{int}):)?(?:($RE{num}{int}):)?($RE{num}{real})$/);

    # Swap hours and minutes if hours are not defined
    if (defined $hrs and not defined $mins) { $mins = $hrs; $hrs = undef; }

    # Set defaults
    $hrs  = 0 if not defined $hrs  or $hrs  !~ m/^$RE{num}{int}$/;
    $mins = 0 if not defined $mins or $mins !~ m/^$RE{num}{int}$/;
    $secs = 0 if not defined $secs or $secs !~ m/^$RE{num}{real}$/;

    # Calculate time value and convert to integer
    my $value = sprintf("%d", ($hrs*3600+$mins*60+$secs) / eval($time_base));

    # Return time value
    return $value;
}

# ------------------------------------------------------------------------------

__END__

=head1 NAME

=head1 SYNOPSIS

=head1 COMMANDS

=head1 OPTIONS

=head1 DESCRIPTION

=head1 HISTORY

=head1 MIT LICENSE

Copyright 2025 by Matthias Boljen

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

=cut
